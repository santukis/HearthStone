import com.santukis.buildsrc.Dependencies
import com.santukis.buildsrc.Modules

plugins {
    id 'com.android.library'
}

apply from: "$rootDir/base_android_module.gradle"

def battlenetProperties = new Properties()
file("battlenet.properties").withInputStream { stream ->
    battlenetProperties.load(stream)
}

android {
    defaultConfig {
        buildConfigField("String", "BATTLENET_CLIENT_ID", battlenetProperties.getProperty("client_id"))
        buildConfigField("String", "BATTLENET_CLIENT_SECRET", battlenetProperties.getProperty("client_secret"))

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }
}

dependencies {
    implementation project(Modules.Entities)
    implementation project(Modules.Repositories)

    implementation Dependencies.Squareup.moshi
    kapt Dependencies.Squareup.moshiCodegen
    implementation Dependencies.Squareup.moshiConverter
    implementation Dependencies.Squareup.retrofit
    implementation Dependencies.Squareup.okhttp

    implementation Dependencies.Androidx.room
    implementation Dependencies.Androidx.roomKotlin
    annotationProcessor Dependencies.Androidx.roomCompiler
    kapt Dependencies.Androidx.roomCompiler

    implementation Dependencies.Androidx.encryptedPreferences

    testImplementation Dependencies.Test.junit5
    testImplementation Dependencies.Test.mockWebServer
    testImplementation Dependencies.Test.mockK

    androidTestImplementation Dependencies.Androidx.room
}